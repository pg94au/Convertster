; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; Non-commercial use only

#define MyAppName "Convertster"
#define MyAppVersion "1.2"
#define MyAppPublisher "Paul Grebenc"
#define MyAppURL "https://github.com/pg94au/Convertster"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{CE398A7C-BF22-4277-A35B-08130D4AE6BE}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
; Require at least Windows 10
MinVersion=10.0
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
ArchitecturesAllowed=x64
ArchitecturesInstallIn64BitMode=x64
DefaultDirName={autopf}\{#MyAppName}
DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=..\LICENSE
PrivilegesRequired=admin
OutputBaseFilename={#MyAppName}-{#MyAppVersion}-Setup
SolidCompression=yes
WizardStyle=modern dynamic
//ShowLanguageDialog=auto
ShowLanguageDialog=true
CloseApplicationsFilter=explorer.exe

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "fr"; MessagesFile: "compiler:Languages\French.isl"
Name: "es"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "de"; MessagesFile: "compiler:Languages\German.isl"
Name: "it"; MessagesFile: "compiler:Languages\Italian.isl"

[CustomMessages]
; English (default)
RestartWarning=This installer will restart Windows Explorer to activate the context menu extension.%n%n \
    Any open File Explorer windows will be closed.%n%n \
    Do you want to continue?
; French
fr.RestartWarning=Ce programme d'installation va redémarrer l'Explorateur Windows pour activer l'extension du menu contextuel.%n%n \
    Toutes les fenêtres de l'Explorateur de fichiers ouvertes seront fermées.%n%n \
    Voulez-vous continuer?
; Spanish
es.RestartWarning=Este instalador reiniciará el Explorador de Windows para activar la extensión del menú contextual.%n%n \
    Todas las ventanas abiertas del Explorador de archivos se cerrarán.%n%n \
    ¿Desea continuar?
; German
de.RestartWarning=Dieses Installationsprogramm startet den Windows Explorer neu, um die Kontextmenü-Erweiterung zu aktivieren.%n%n \
    Alle geöffneten Datei-Explorer-Fenster werden geschlossen.%n%n \
    Möchten Sie fortfahren?
; Italian
it.RestartWarning=Questo programma di installazione riavvierà Esplora risorse per attivare l'estensione del menu contestuale.%n%n \
    Tutte le finestre di Esplora file aperte verranno chiuse.%n%n \
    Vuoi continuare?

; VC++ Runtime Error Messages
VCRuntimeMissingError=Internal installer error: VC++ runtime missing.
VCRuntimeInstallFailedError=Failed to install required Microsoft Visual C++ runtime.
VCRuntimeInstallationError=Microsoft Visual C++ runtime installation failed.

fr.VCRuntimeMissingError=Erreur interne de l'installateur : runtime VC++ manquant.
fr.VCRuntimeInstallFailedError=Échec de l'installation du runtime Microsoft Visual C++ requis.
fr.VCRuntimeInstallationError=L'installation du runtime Microsoft Visual C++ a échoué.

es.VCRuntimeMissingError=Error interno del instalador: falta el runtime de VC++.
es.VCRuntimeInstallFailedError=Error al instalar el runtime de Microsoft Visual C++ requerido.
es.VCRuntimeInstallationError=La instalación del runtime de Microsoft Visual C++ falló.

de.VCRuntimeMissingError=Interner Installationsfehler: VC++ Runtime fehlt.
de.VCRuntimeInstallFailedError=Fehler beim Installieren der erforderlichen Microsoft Visual C++ Runtime.
de.VCRuntimeInstallationError=Die Installation der Microsoft Visual C++ Runtime ist fehlgeschlagen.

it.VCRuntimeMissingError=Errore interno dell'installazione: runtime VC++ mancante.
it.VCRuntimeInstallFailedError=Impossibile installare il runtime Microsoft Visual C++ richiesto.
it.VCRuntimeInstallationError=Installazione del runtime Microsoft Visual C++ non riuscita.

[Files]
; The DLL for the explorer extension.
Source: "..\ShellExtContextMenuHandler\x64\Release\CppShellExtContextMenuHandler.dll"; DestDir: "{sys}"; Flags: regserver
; The image converter application.
Source: "..\ImageConverter\bin\Release\ImageConverter.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\ImageConverter\bin\Release\ImageConverter.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\ImageConverter\bin\Release\*.dll"; DestDir: "{app}"; Flags: ignoreversion
; Satellite resource assemblies for localization (fr, es, and any future cultures)
Source: "..\ImageConverter\bin\Release\*.resources.dll"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

; Required to install the VC++ runtime that is needed by the explorer extension.
Source: "Prerequisites\VC_redist.x64.exe"; DestDir: "{tmp}"; Flags: dontcopy
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKLM; Subkey: "Software\Convertster"; ValueType: string; ValueName: "ExecutablePath"; ValueData: "{app}\ImageConverter.exe"; Flags: uninsdeletekey

[Code]
function InitializeSetup(): Boolean;
var
  ResultCode: Integer;
begin
  ResultCode := MsgBox(
    CustomMessage('RestartWarning'),
    mbConfirmation,
    MB_YESNO or MB_DEFBUTTON2);

  if ResultCode = IDYES then
    Result := True
  else
    Result := False;
end;

function IsVCRuntimeInstalled: Boolean;
var
  Major: Cardinal;
begin
  Result :=
    RegQueryDWordValue(
      HKLM,
      'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64',
      'Installed',
      Major) and (Major = 1);
end;

function PrepareToInstall(var NeedsRestart: Boolean): String;
var
  ResultCode: Integer;
  Path: string;
begin
  if not IsVCRuntimeInstalled then
  begin
    ExtractTemporaryFile('VC_redist.x64.exe');
    Path := ExpandConstant('{tmp}\VC_redist.x64.exe');
    Log('Checking for VC++ runtime at: ' + Path);

    if not FileExists(Path) then
    begin
      Log('ERROR: VC_redist.x64.exe does not exist');
      Result := CustomMessage('VCRuntimeMissingError');
      Exit;
    end;
  
    if not Exec(
      ExpandConstant('{tmp}\VC_redist.x64.exe'),
      '/install /quiet /norestart',
      '',
      SW_SHOW,
      ewWaitUntilTerminated,
      ResultCode)
    then
    begin
      Result := CustomMessage('VCRuntimeInstallFailedError');
      Exit;
    end;

    if ResultCode <> 0 then
    begin
      Result := CustomMessage('VCRuntimeInstallationError');
      Exit;
    end;
  end;

  Result := '';
end;

[UninstallRun]
Filename: "taskkill"; Parameters: "/f /im explorer.exe"; Flags: runhidden

Filename: "{win}\explorer.exe"; Flags: nowait
